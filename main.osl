def "switchslime" "type,player"
if type == "basic" (
  out_speed = 4
  out_knockback = 2
  out_weight = 1.75
  out_size = 1
)
if type == "heavy" (
  out_speed = 3
  out_knockback = 4
  out_weight = 2
  out_size = 1.5
)
if type == "light" (
  out_speed = 5
  out_knockback = 1
  out_weight = 1.5
  out_size = 1
)
if player == 1 (
  player1_type = type
  player1_speed = out_speed
  player1_knockback = out_knockback
  player1_gravity = out_weight
  player1_size = out_size
)
if player == 2 (
  player2_type = type
  player2_speed = out_speed
  player2_knockback = out_knockback
  player2_gravity = out_weight
  player2_size = out_size
)
endef

def "reset_data"
  // player main data
  player1_x = -100
  player1_y = 50
  player1_xvel = 0
  player1_yvel = 0
  player1_health = 0
  player1_lives = 3
  player1_dead = false
  player1_ifrm = 0

  player2_x = 100
  player2_y = 50
  player2_xvel = 0
  player2_yvel = 0
  player2_health = 0
  player2_dead = false
  player2_ifrm = 0

  // player animation variables
  player1_anim = "idle"
  player1_frame = 1
  player1_endfrm = 3

  player2_anim = "idle"
  player2_frame = 1
  player2_endfrm = 3

  // player display variables
  player1_stretch = -100
  player1_target = -100
  player1_grounded = true
  player1_dir = "r"

  player2_stretch = 100
  player2_target = 100
  player2_grounded = true
  player2_dir = "l"

  player1_ability = "none"
  player1_ability_lastused = 0

  player2_ability = "none"  
  player2_ability_lastused = 0

  shake_x = 0
  shake_y = 0
  shake_x_vel = 0
  shake_y_vel = 0

  p1_lst = timer
  p2_lst = timer

  cam_x = 0
  cam_y = 0
  stage_x = 0
  stage_y = 0
endef

main_image = [0,3].random

types = {}
types.key("basic") = "The Basic Slime :/"
types.key("heavy") = "Slower, heavier, less knockback"
types.key("light") = "Faster, lighter, more knockback"

curlast = 0
curframe = 0

switchslime "basic" 1
switchslime "basic" 2

window "dimensions" 1200 700
window "show"

github = "https://raw.githubusercontent.com/Mistium/Super-Slime-Bros/main/"

stage = 0
selected_stage = 0
page = "main"

window_colour = #fff
zoom = 1

def "p1_idle"
  player1_anim = "idle"
  player1_frame = 1
  player1_endfrm = 3
endef

def "p1_attack"
  if timer - player2_ifrm > 0.5 (
    player2_health += 2
    player2_yvel += player2_health + 30 / player2_knockback / 10
    player2_y += 40
    if player1_dir == "r" "player2_xvel += player2_health + 30 / player2_knockback"
    if player1_dir == "l" "player2_xvel -= player2_health + 30 / player2_knockback"
  )
  player2_ifrm = timer
  pause = timer + 0.1
endef

def "p2_idle"
  player2_anim = "idle"
  player2_frame = 1
  player2_endfrm = 3
endef

def "p2_attack"
  if timer - player1_ifrm > 0.5 (
    player1_health += 2
    player1_yvel += player1_health + 30 / player1_knockback / 10
    player1_y += 40
    if player2_dir == "r" "player1_xvel += player1_health + 30 / player1_knockback"
    if player2_dir == "l" "player1_xvel -= player1_health + 30 / player1_knockback"
  )
  player1_ifrm = timer
  pause = timer + 0.1
endef

def "attack" "player,attack_type"
  if player == 1 and ( attack_type == 1 ) (
    goto player1_x player1_y
    change -40 player1_xvel
    if player1_dir == "r" "change 80"
    hitbox 200 150 player2_x player2_y
    if collided "p1_attack"
  )
  if player == 2 and ( attack_type == 1 ) (
    goto player2_x player2_y
    change -40 player2_vel
    if player2_dir == "r" "change 80"
    hitbox 200 150 player1_x player1_y
    if collided "p2_attack"
  )
endef

def "step"
  if player1_anim == "attack1" and ( player1_frame == 1 ) "p1_idle"

  if player1_anim != "idle" and ( player1_anim != "attack1" ) and "d".pressed.not and "a".pressed.not (
    p1_idle
  )
  if ["walk","attack1"].contains(player1_anim).not and ( "d".pressed or "a".pressed ) (
    player1_anim = "walk"
    player1_frame = 1
    player1_endfrm = 6
  )
  if player1_anim != "attack1" and "space".pressed (
    player1_anim = "attack1"
    player1_frame = 1
    player1_endfrm = 7
  )

  if player2_anim == "attack1" and ( player2_frame == 1 ) "p2_idle"
  if player2_anim != "idle" and ( player2_anim != "attack1" ) and "right arrow".pressed.not and "left arrow".pressed.not (
    p2_idle
  )
  if ["walk","attack1"].contains(player2_anim).not and ( "right arrow".pressed or "left arrow".pressed ) (
    player2_anim = "walk"
    player2_frame = 1
    player2_endfrm = 6
  )
  if player2_anim != "attack1" and "enter".pressed (
    player2_anim = "attack1"
    player2_frame = 1
    player2_endfrm = 7
  )

  player1_target = -100
  if player1_dir == "r" "player1_target = 100"

  player2_target = -100
  if player2_dir == "r" "player2_target = 100"

  // increase speed while on ground
  player1_speedmod = player1_speed
  if player1_grounded "player1_speedmod += 2"
  player2_speedmod = player2_speed
  if player2_grounded "player2_speedmod += 2"

  if "d".pressed (
    player1_xvel += player1_speedmod
    player1_dir = "r"
  )
  if "a".pressed (
    player1_xvel -= player1_speedmod
    player1_dir = "l"
  )
  if "w".pressed and player1_grounded (
    player1_yvel = 30
    player1_grounded = false
    player1_stretch *= 0.3
    player1_target *= 0.1
)

  if "right arrow".pressed (
    player2_xvel += player2_speedmod
    player2_dir = "r"
  )
  if "left arrow".pressed (
    player2_xvel -= player2_speedmod
    player2_dir = "l"
  )
  if "up arrow".pressed and player2_grounded (
    player2_yvel = 30
    player2_grounded = false
    player2_stretch *= 0.3
    player2_target *= 0.1
  )

  player1_yvel -= player1_gravity
  player1_xvel *= 0.8
  player2_yvel -= player2_gravity
  player2_xvel *= 0.8

  player1_x += player1_xvel
  player1_y += player1_yvel
  player2_x += player2_xvel
  player2_y += player2_yvel

  player1_stretch += player1_target - player1_stretch / 4
  player2_stretch += player2_target - player2_stretch / 4

  if timer - 0.15 > p1_lst (
    player1_frame += 1
    p1_lst = timer
  )
  if timer - 0.15 > p2_lst (
    player2_frame += 1
    p2_lst = timer
  )

  if player1_frame > player1_endfrm "player1_frame = 1"
  if player2_frame > player2_endfrm "player2_frame = 1"

  if player1_anim == "attack1" "attack 1 1"
  if player2_anim == "attack1" "attack 2 1"
endef

def "render_players"
  goto player1_x + cam_x player1_y + cam_y + 20
  if player1_type == "heavy" "change_y 25"
  stretch "x" player1_stretch.int
  if timer - player1_ifrm > 0.5 or ( player1_frame.round % 2 == 0 ) (
    image github ++ "assets/" ++ player1_type ++ "/" ++ player1_anim ++ "/" ++ player1_frame.round ++ ".png" 200 * player1_size
  )
  change 0 60
  square 20 20 10 : c#8a3d34
  text "1" 10 : c#fff chx#-5
  goto player2_x + cam_x player2_y + cam_y + 20
  if player2_type == "heavy" "change_y 25"
  stretch "x" player2_stretch.int
  if timer - player2_ifrm > 0.5 or ( player2_frame.round % 2 == 0 ) (
    image github ++ "assets/" ++ player2_type ++ "/" ++ player2_anim ++ "/" ++ player2_frame.round ++ ".png" 200 * player2_size
  )
  change 0 60
  square 20 20 10 : c#345b8a
  text "2" 10 : c#fff chx#-5
endef

def "collide" "width,height,reset,droppable"
c #000
hitbox width * zoom height * zoom player1_x player1_y - 50
if ( "s".pressed.not and "w".pressed.not and droppable ) or droppable.not and collided (
  player1_grounded = true
  player1_y = reset
  player1_yvel = 0
)

hitbox width * zoom height * zoom player2_x player2_y - 50
if ( "down arrow".pressed.not and "up arrow".pressed.not and droppable ) or droppable.not and collided (
  player2_grounded = true
  player2_y = reset
  player2_yvel = 0
)
endef

def ssb_button "txt,location"
  square 360 40 10 : c#fff
  if clicked "page = location"
  m = mouse_touching
  if m "change mouse_x - x_position / 20 mouse_y - y_position / 20"
  if m "square 360 40 20 : c#eee"
  change -170
  text txt 10 : c#000
endef

def "level" "url"
  multiplier = frame_width - 10 / url.imageinfo("width")
  w = url.imageinfo("width") * multiplier - 30
  h = window_height - 100
  if selected_stage == url "square w h 15 : c#000"
  square w h 10 : c#ddd
  if clicked "selected_stage = url"
  frame x_position - ( w / 2 ) y_position + ( h / 2 ) x_position + ( w / 2 ) y_position - ( h / 2 )
  image url frame_width - 40
  frame "clear"
endef

def "slime" "type,anim,selected,p"
  if selected == type "square 80 80 15 : c#000"
  square 80 80 10 : c#eee
  if clicked "switchslime type p"
  image github ++ "assets/" ++ type ++ "/" ++ anim ++ "/" ++ curframe.round ++ ".png" 130
endef

def "ability" "type,selected,p"
  if selected == type "square 80 80 15 : c#000"
  square 80 80 10 : c#eee
  if clicked "switchability type p"
  image github ++ "assets/abilities/" ++ type ++ ".png" 130
endef

def "screenshake" "x_mag,y_mag"
  shake_x_vel += x_mag * 10
  shake_y_vel += y_mag * 10
endef

def "main"
if stage == 0 (
  loc 2 999 210 0
  square 400 window_height - 30 10 : c#ddd
  if page == "main" (
    loc 2 2 30 -50
    text "Super Slime Bros" 20 : c#000

    loc 2 2 210 -120
    ssb_button "Start Battle" "level_select"
    lvl_page = 1

    loc 2 2 210 -180
    ssb_button "Quit Game" "quit"

    loc 2 -2 210 210
    image github ++ "assets/ui/controls.png" 210
    loc -5 -5 mouse_x / 10 mouse_y / 10
    hzoom = zoom / 2
    image github ++ "assets/stages/spike.png" window_width + 700 * hzoom

  )
  if page == "level_select" (
    loc 2 2 30 -50
    text "Level Select" 20 : c#000

    loc 2 -2 210 120
    if selected_stage != 0 "ssb_button Play play"
    txt = "Select a stage to play"
    loc 2 -2 40 120
    if selected_stage == 0 "text txt 10"
    loc 2 -2 210 60
    ssb_button "Back to Main Menu" "main"

    if timer - 0.15 > curlast "curframe += 1"
    if timer - 0.15 > curlast "curlast = timer"
     if curframe > 6 "curframe = 1"

    c #888
    loc 2 2 0 -80
    x = x_position
    y = y_position
    loc 2 -2 400 150
    frame x y x_position y_position 750

    loc 2 2 190 -190 + scroll_y
    square 280 320 50 : c#fff
    loc 2 2 35 -30 + scroll_y
    text "Player 1" 13 : c#000
    loc 2 2 35 -70 + scroll_y
    text "Slime Type" 10
    loc 2 2 190 -140 + scroll_y
    square 280 80 30 : c#ddd
    change -100
    slime "basic" "walk" player1_type 1
    change 100
    slime "light" "walk" player1_type 1
    change 100
    slime "heavy" "walk" player1_type 1
    loc 2 2 35 -210 + scroll_y
    text types.key(player1_type).destr 8 : c#000

    loc 2 2 35 -240 + scroll_y
    text "Slime Ability" 10
    loc 2 2 190 -310 + scroll_y
    square 280 80 30 : c#ddd
    change -100
    ability "dash" player1_ability 1
    change 100
    ability "heal" player1_ability 1
    change 100
    ability "" player1_ability 1

    loc 2 2 190 -570 + scroll_y
    square 280 320 50 : c#fff
    loc 2 2 35 -410 + scroll_y
    text "Player 2" 13 : c#000
    loc 2 2 35 -450 + scroll_y
    text "Slime Type" 10
    loc 2 2 190 -520 + scroll_y
    square 280 80 30 : c#ddd
    change -100
    slime "basic" "walk" player2_type 2
    change 100
    slime "light" "walk" player2_type 2
    change 100
    slime "heavy" "walk" player2_type 2
    loc 2 2 35 -590 + scroll_y
    text types.key(player2_type).destr 8 : c#000

    loc 2 2 35 -620 + scroll_y
    text "Slime Ability" 10
    loc 2 2 190 -690 + scroll_y
    square 280 80 30 : c#ddd
    change -100
    ability "dash" player2_ability 2
    change 100
    ability "heal" player2_ability 2
    change 100
    ability "" player2_ability 2

    frame "clear"
    loc 999 -2 200 40
    text "<lvl_page>/<tot_lvls> (<current_level_view>)" 10 : c#000
    loc 999 999 220 30
    frame_width = window_width - 450
    tot_lvls = 3
    if lvl_page == 1 and ( stage == 0 ) (
      level github ++ "assets/stages/spike.png"
      current_level_view = "Spike"
    )
    if lvl_page == 2 and ( stage == 0 ) (
      level github ++ "assets/stages/tree.png"
      current_level_view = "Tree"
    )
    if lvl_page == 3 and ( stage == 0 ) (
      level github ++ "assets/stages/mushroom.png"
      current_level_view = "Mushroom"
    )
    if page == "level_select" (
      loc -2 -2 -40 40
      square 30 30 15 : c#000
      square 30 30 10 : c#ddd
      icon "right-arrow" 1 : c#000
      if mouse_touching "cursor pointer"
      if clicked and can (
        can = false
        lvl_page += 1
      )
    )
    if page == "level_select" (
      loc 2 -2 480 40
      square 30 30 15 : c#000
      square 30 30 10 : c#ddd
      icon "left-arrow" 1 : c#000
      if mouse_touching "cursor pointer"
      if clicked and can (
        can = false
        lvl_page -= 1
      )
    )
    if lvl_page < 1 "lvl_page = tot_lvls"
    if lvl_page > tot_lvls "lvl_page = 1"
  )
  if page == "play" (
    page = 0
    stage = selected_stage
    reset_data
  )
  if page == "quit" (
    window "stop"
  )
)
if stage != 0 (
  cam_x_target = player1_x + player2_x / -2
  cam_y_target = player1_y + player2_y / -2
  shake_x += shake_x_mag
  shake_y += shake_y_mag
  shake_x_mag -= shake_x_mag * 1.5
  shake_y_mag -= shake_y_mag * 1.5
  cam_x += cam_x_target - cam_x / 10
  cam_y += cam_y_target - cam_y / 10

  stage_x = cam_x + shake_x
  stage_y = cam_y + shake_y

  direction 90
  stretch "x" 100
  goto stage_x stage_y
  if stage.contains("tree") "change_y 60"
  if stage.contains("mushroom") "change_y 105"
  image stage 1000 * zoom

  step

  if player1_y < -1000 (
    player1_deadtime = timer
    player2_dead = true
  )
  if player2_y < -1000 (
    player2_deadtime = timer
    player2_dead = true
  )
  if timer - player1_deadtime > 5 and player1_dead (
    player1_x = 0
    player1_y = 500
    player1_xvel = 0
    player1_yvel = 0
    player1_lives -= 1
  )
  if timer - player2_deadtime > 5 and player2_dead (
    player2_x = 0
    player2_y = 500
    player2_xvel = 0
    player2_yvel = 0
    player2_lives -= 1
  )
)
if stage != 0 (
  c #ddd
  loc 2 -2 80 50
  square 150 50 10 1
  c #000
  loc 2 -2 20 50
  text "<player1_health>%" 15
  loc 2 -2 110 50
  text "<player2_health>%" 15
  loc 2 2 35 -35
  square 30 30 15 : c#000
  square 30 30 10 : c#ddd
  icon "home" 1 : c#000
  if clicked (
    stage = 0
    page = "main"
  )
)
if stage.contains("spike") (
  c #000
  goto 0 -30
  collide 1000 60 30 false

  goto 250 165
  collide 200 40 210 true

  goto -320 195
  collide 200 40 240 true

  goto -20 330
  collide 160 40 380 true
)
if stage.contains("tree") (
  c #000
  goto 0 -30
  collide 1000 60 30 false

  goto 350 120
  collide 100 40 180 true

  goto -320 195
  collide 200 40 240 true

  goto -20 330
  collide 160 40 380 true
)
if stage.contains("mushroom") (
  goto 0 -30
  collide 1000 60 30 false

  goto -260 80
  collide 80 40 130 true

  goto -340 290
  collide 190 40 350 true

  goto -160 240
  collide 110 40 290 true

  goto -60 260
  collide 110 60 310 true

  goto 30 280
  collide 110 60 350 true
  
  goto 300 380
  collide 190 40 430 true
)
if stage != 0 (
  render_players
)
if mouse_down.not "can = true"
endef

reset_data

mainloop:

main

frame "clear"
