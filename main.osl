player1_x = -100
player1_y = 50
player1_xvel = 0
player1_yvel = 0
player1_type = "basic"
player1_anim = "idle"
player1_frame = 1
player1_endfrm = 3
player1_health = 0
player1_stretch = -100
player1_target = -100
player1_grounded = true
player1_dir = "r"

player2_x = 100
player2_y = 50
player2_xvel = 0
player2_yvel = 0
player2_type = "basic"
player2_anim = "idle"
player2_frame = 1
player2_endfrm = 3
player2_health = 0
player2_stretch = 100
player2_target = 100
player2_grounded = true
player2_dir = "l"

p1_lst = timer
p2_lst = timer

window "dimensions" 1200 700

github = "https://raw.githubusercontent.com/Mistium/Super-Slime-Bros/main/"

stage = 0
selected_stage = 0
page = "main"

cam_x = 0
cam_y = 0
stage_x = 0
stage_y = 0

window_colour = #fff
zoom = 1

def "p1_idle"
  player1_anim = "idle"
  player1_frame = 1
  player1_endfrm = 3
endef

def "p2_idle"
  player2_anim = "idle"
  player2_frame = 1
  player2_endfrm = 3
endef

def "step"
  if player1_anim == "attack1" and ( player1_frame == 1 ) "p1_idle"
  if player1_anim != "idle" and ( player1_anim != "attack1" ) and "d".pressed.not and "a".pressed.not (
    p1_idle
  )
  if player1_anim != "walk" and ( "d".pressed or "a".pressed ) (
    player1_anim = "walk"
    player1_frame = 1
    player1_endfrm = 6
  )
  if player1_anim != "attack1" and "space".pressed (
    player1_anim = "attack1"
    player1_frame = 1
    player1_endfrm = 9
  )

  if player2_anim == "attack1" and ( player2_frame == 1 ) "p2_idle"
  if player2_anim != "idle" and ( player2_anim != "attack1" ) and "right arrow".pressed.not and "left arrow".pressed.not (
    p2_idle
  )
  if player2_anim != "walk" and ( "right arrow".pressed or "left arrow".pressed ) (
    player2_anim = "walk"
    player2_frame = 1
    player2_endfrm = 6
  )
  if player2_anim != "attack1" and "enter".pressed (
    player2_anim = "attack1"
    player2_frame = 1
    player2_endfrm = 9
  )

  player1_target = -100
  if player1_dir == "r" "player1_target = 100"

  player2_target = -100
  if player2_dir == "r" "player2_target = 100"

  if "d".pressed (
    player1_xvel = 15
    player1_dir = "r"
  )
  if "a".pressed (
    player1_xvel = -15
    player1_dir = "l"
  )
  if "w".pressed and player1_grounded (
    player1_yvel = 30
    player1_grounded = false
    player1_stretch *= 0.5
)

  if "right arrow".pressed (
    player2_xvel = 15
    player2_dir = "r"
  )
  if "left arrow".pressed (
    player2_xvel = -15
    player2_dir = "l"
  )
  if "up arrow".pressed and player2_grounded (
    player2_yvel = 30
    player2_grounded = false
    player2_stretch *= 0.5
  )

  player1_yvel -= 2
  player1_xvel *= 0.8
  player2_yvel -= 2
  player2_xvel *= 0.8

  player1_x += player1_xvel
  player1_y += player1_yvel
  player2_x += player2_xvel
  player2_y += player2_yvel

  player1_stretch += player1_target - player1_stretch / 4
  player2_stretch += player2_target - player2_stretch / 4

  if timer - 0.1 > p1_lst (
    player1_frame += 1
    p1_lst = timer
  )
  if timer - 0.1 > p2_lst (
    player2_frame += 1
    p2_lst = timer
  )

  if player1_frame > player1_endfrm "player1_frame = 1"
  if player2_frame > player2_endfrm "player2_frame = 1"
endef

def "render_players"
  goto player1_x + cam_x player1_y + cam_y
  stretch "x" player1_stretch.int
  image github ++ "assets/" ++ player1_type ++ "_" ++ player1_anim ++ "/" ++ player1_frame.round ++ ".png" 200
  goto player2_x + cam_x player2_y + cam_y
  stretch "x" player2_stretch.int
  image github ++ "assets/" ++ player2_type ++ "_" ++ player2_anim ++ "/" ++ player2_frame.round ++ ".png" 200
endef

def "collide" "width,height,reset,droppable"
c #000
hitbox width * zoom height * zoom player1_x player1_y - 50
if ( ( "s".pressed.not and droppable ) or droppable.not ) and collided (
  player1_grounded = true
  player1_y = reset
  player1_yvel = 0
)

hitbox width * zoom height * zoom player2_x player2_y - 50
if ( ( "down arrow".pressed.not and droppable ) or droppable.not ) and collided (
  player2_grounded = true
  player2_y = reset
  player2_yvel = 0
)
endef

def ssb_button "txt,location"
  square 360 40 10 : c#fff
  if clicked "page = location"
  m = mouse_touching
  if m "change mouse_x - x_position / 20 mouse_y - y_position / 20"
  if m "square 360 40 20 : c#eee"
  change -170
  text txt 10 : c#000
endef

def "level" "url"
  multiplier = frame_width - 10 / url.imageinfo("width")
  page_len += url.imageinfo("height") * multiplier
  if selected_stage == url (
    square url.imageinfo("width") * multiplier - 30 url.imageinfo("height") * multiplier 20 : c#000
  )
  square url.imageinfo("width") * multiplier - 30 url.imageinfo("height") * multiplier 10 : c#ddd
  if clicked "selected_stage = url"
  image url frame_width - 40
  change_y url.imageinfo("height") * multiplier * -1 + 30
endef

mainloop:

if stage == 0 (
  loc 2 999 210 0
  square 400 window_height - 30 10 : c#ddd
  if page == "main" (
    loc 2 2 30 -50
    text "Super Slime Bros" 20 : c#000

    loc 2 2 210 -120
    ssb_button "Start Battle :O" "level_select"

    loc 2 2 210 -180
    ssb_button "Quit Game" "quit"

    loc -5 -5 mouse_x / 10 mouse_y / 10
    image github ++ "assets/stages/spike.png" 1000 * zoom
  )
  if page == "level_select" (
    loc 2 2 30 -50
    text "Level Select" 20 : c#000

    loc 2 -2 210 120
    if selected_stage != 0 "ssb_button Play play"
    txt = "Select a stage to play"
    loc 2 -2 40 120
    if selected_stage == 0 "text txt 10"
    loc 2 -2 210 60
    ssb_button "Back to Main Menu" "main"

    loc 2 2 440 -30
    x = x_position
    y = y_position
    loc -2 -2 0 0
    c #eee
    frame x y x_position y_position page_len

    page_len = 0
    url = github ++ "assets/stages/spike.png"
    start = frame_width - 10 / url.imageinfo("width") * url.imageinfo("height") / -2
    loc 999 2 0 start + scroll_y - 30
    level url
    frame "clear"
  )
  if page == "play" (
    page = 0
    stage = selected_stage
  )
  if page == "quit" (
    window "stop"
  )
)
if stage != 0 (
  cam_x_target = player1_x + player2_x / -2
  cam_y_target = player1_y + player2_y / -2

  cam_x += cam_x_target - cam_x / 6
  cam_y += cam_y_target - cam_y / 6

  stage_x = cam_x
  stage_y = cam_y

  stretch "x" 100
  goto stage_x stage_y
  image stage 1000 * zoom

  step
)
if stage == github ++ "assets/stages/spike.png" (
  c #000
  goto 0 0
  collide 1000 30 50 false

  goto 250 165
  collide 200 40 230 true

  goto -320 195
  collide 200 40 260 true

  goto -20 330
  collide 160 40 400 true
)
if stage != 0 (
  render_players
)

import "win-buttons"
